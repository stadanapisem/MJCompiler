package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import rs.ac.bg.etf.pp1.ast.*;

// *****************  KEYWORDS **********************
terminal PROGRAM, CONST, VOID, BREAK, CLASS, IF, ELSE, NEW, RETURN, DO, WHILE, EXTENDS;
terminal CONTINUE, PRINT, READ, THIS;

// *****************  OPERATORS *********************
terminal PLUS, MINUS, MUL, DIV, MOD, EQ, NEQ, GRT, GRTEQ, LSS, LEQ, AND, OR, ASSIGN, INC, DEC;
terminal SEMI, COMMA, DOT, LPAREN, RPAREN, RBRACE, LBRACE, LSQUARE, RSQUARE;

// *****************  IDENTIFICATORS ****************
terminal int NUMBER;
terminal Boolean BOOL;
terminal char CHAR;
terminal String IDENT;

// *****************  NONTERMINALS  *****************
nonterminal program; nonterminal rs.etf.pp1.symboltable.concepts.Obj program_name;

nonterminal rs.etf.pp1.symboltable.concepts.Struct type, method_return_type;

nonterminal declaration_section, declaration,
            const_declaration_line, const_id_list, const_identifier,
            var_declaration_line, var_declaration_list, var_id_list, var_id, var_identifier,
            optional_extends,
            method_decl_section, optional_method_decl_section, method_decl_list, method_decl,  method_definition,
            formal_parameter_section, formal_parameter_list, formal_parameter;
nonterminal rs.etf.pp1.symboltable.concepts.Obj class_declaration, class_identifier;
nonterminal rs.etf.pp1.symboltable.concepts.Obj const_id, method_identifier;
nonterminal rs.etf.pp1.symboltable.concepts.Obj constant, char_const, numeric_const, bool_const;

nonterminal statement_list, statement, print_statement, read_statement, return_statement,
            if_statement, if_statement_header, do_statement, break_statement, continue_statement,
            optional_else, else_jmp_fix, remember_pc;
nonterminal designator_statement, actual_parameter, actual_parameter_section, actual_parameter_list, method_call_ident;
nonterminal rs.etf.pp1.symboltable.concepts.Obj designator, array_ident, array_fld_ident, array_fld_header, array_fld_fix;
nonterminal java.lang.String addition_operator, multiplication_operator;
nonterminal rs.etf.pp1.symboltable.concepts.Struct term, factor, expression, addition_term_list, multiplication_factor_list;
nonterminal rs.etf.pp1.symboltable.concepts.Struct condition, condition_term_list, condition_term, condition_factor_list, condition_factor;
nonterminal java.lang.String rel_operator, or_operator, and_operator;
precedence left ELSE;
// **************************************************
//
// PRODUCTIONS
//
// *****************  PROGRAM  **********************
program ::= (Program) PROGRAM program_name:pName declaration_section method_decl_section;
program_name ::= (ProgramName) IDENT:id;

// *****************  TYPE **************************
type ::= (Type) IDENT: typeName;
method_return_type ::= (MethodReturnType) type
                     | (VoidReturnType) VOID;

// *****************  DECLARATIONS ******************
declaration_section ::= (DeclarationSection) declaration_section declaration
                   | (NoDeclarations) /* epsilon */;

  declaration ::= (DeclarationConst) const_declaration_line
                | (DeclarationVar) var_declaration_line
                | (DeclarationClass) class_declaration;

    // *** CONSTANT DECLARATION
    const_declaration_line ::= (ConstDeclarationLine) CONST type: t const_id_list SEMI;
      const_id_list ::= (ConstIdList) const_id_list COMMA const_id
                      | (ConstIdTerm) const_id;
        const_id ::= (ConstId) const_identifier ASSIGN constant;
        const_identifier ::= (ConstIdentifier) IDENT: id;
        constant ::= (ConstNumber) numeric_const: var
                   | (ConstBool) bool_const: var
                   | (ConstChar) char_const: var;
          numeric_const ::= (NumericConst) NUMBER: var;
          bool_const ::= (BoolConst) BOOL: var;
          char_const ::= (CharConst) CHAR: var;

    // *** VARIABLES DECLARATION
    var_declaration_list ::= (VarDeclarationList) var_declaration_list var_declaration_line
                           | (NoVariableDeclarations) /* epsilon */;
      var_declaration_line ::= (VarDeclarationLine) type: t var_id_list SEMI;
      var_id_list ::= (VarIDList) var_id_list COMMA var_id
                    | (VarIDTerm) var_id;
        var_id ::= (VarID) var_identifier: ident
                 | (VarIDArray) var_identifier: ident LSQUARE RSQUARE;
           var_identifier ::= (VarIdentifier) IDENT: id;

    // *** CLASS DECLARATION
    class_declaration ::= (ClassDeclaration) CLASS class_identifier optional_extends LBRACE var_declaration_list optional_method_decl_section RBRACE;
      class_identifier ::= (ClassIdentifier) IDENT: id;
      optional_extends ::= (Extends) EXTENDS type
                         | /* epsilon */;

    // *** METHOD DECLARATION
    optional_method_decl_section ::= (OptionalMethodDeclSection) method_decl_section
                                   | (NoMethodSection)/* epsilon */;
    method_decl_section ::= (MethodDeclSection) LBRACE method_decl_list RBRACE;
      method_decl_list ::= (MethodDeclList) method_decl_list method_definition
                         | (MethodTerm) /* epsilon */;
        method_decl ::= (MethodDeclaration) method_identifier LPAREN formal_parameter_section RPAREN var_declaration_list;
          method_identifier ::= (MethodIdentifier) method_return_type IDENT: id;
          formal_parameter_section ::= (FormalParameters) formal_parameter_list
                                     | (NoParameters) /* epsilon */;
            formal_parameter_list ::= (FormalParameterList) formal_parameter_list COMMA formal_parameter
                                    | (FormalTermParameter) formal_parameter;
              formal_parameter ::= (FormalParameter) type IDENT
                                 | (FormalParameterArray) type IDENT LSQUARE RSQUARE;
        method_definition ::= (MethodDefinition)  method_decl LBRACE statement_list RBRACE;

// ***************** STATEMENTS *********************
statement_list ::= (StatementList) statement_list statement
                 | (NoStatements) /* epsilon */;
  statement ::= designator_statement SEMI
              | read_statement SEMI
              | print_statement SEMI
              | return_statement SEMI
              | if_statement
              | (DoStatement) do_statement SEMI
              | (BreakStatement) break_statement SEMI
              | (ContinueStatement) continue_statement SEMI
              | LBRACE statement_list RBRACE;
    return_statement ::= (ReturnStatement) RETURN expression
                       | (ReturnVoid) RETURN;
    read_statement ::= (ReadStatement) READ LPAREN designator RPAREN;
    print_statement ::= (PrintStatement) PRINT LPAREN expression RPAREN
                      | (ComplexPrintStatement) PRINT LPAREN expression COMMA numeric_const RPAREN;
    if_statement ::= (IfStatement) if_statement_header statement optional_else;
      if_statement_header ::= IF LPAREN condition RPAREN;
      optional_else ::= else_jmp_fix statement
                      | /* epsilon */;
        else_jmp_fix ::= (ElseFix) ELSE;
    do_statement ::= remember_pc statement WHILE LPAREN condition RPAREN;
        remember_pc ::= (RememberPc) DO;
    break_statement ::= BREAK;
    continue_statement ::= CONTINUE;

// ***************** DESIGNATORS ********************
designator_statement ::= (Assignment) designator ASSIGN expression
                       | (Increment) designator INC
                       | (Decrement) designator DEC
                       | (MethodCallDesignator) method_call_ident actual_parameter_section RPAREN;
    method_call_ident ::= (MethodCallIdent) designator LPAREN;
  designator ::= (DesignatorSingle) IDENT: id
               | (DesignatorArray) array_ident expression RSQUARE
               | (DesignatorFieldSingle) designator DOT IDENT: id
               | (DesignatorArrayFld) array_fld_header array_fld_fix
               | (DesignatorThis) THIS DOT IDENT: id;
    array_ident ::= (ArrayIdent) IDENT: id LSQUARE;
    array_fld_ident ::= (ArrayFldIdent) IDENT: id LSQUARE;
      array_fld_header ::= (DesignatorFieldArray) designator DOT array_fld_ident;
      array_fld_fix ::= expression RSQUARE;
    actual_parameter_section ::= actual_parameter_list
                               | /* epsilon */;
      actual_parameter_list ::= actual_parameter_list COMMA actual_parameter
                              | actual_parameter;
        actual_parameter ::= (ActualParameter) expression;

// ***************** EXPRESSIONS ********************
expression ::= (AddExpression) addition_term_list;
  addition_term_list ::= (TermList) addition_term_list addition_operator term
                       | (TerminalTerm) term
                       | (NegTerminalTerm) MINUS term;
    addition_operator ::= (SignAdd) PLUS: op
                        | (SignSub) MINUS: op;
  term ::= (Term) multiplication_factor_list;
  multiplication_factor_list ::= (FactorList) multiplication_factor_list multiplication_operator factor
                               | (TerminalFactor) factor;
    multiplication_operator ::= (SignMul) MUL: op
                              | (SignDiv) DIV: op
                              | (SignMod) MOD: op;
  factor ::= (ConstantFactor) constant
           | (ExpressionFactor) LPAREN expression RPAREN
           | (ConstructorFactor) NEW type
           | (ConstructorArrayFactor) NEW type LSQUARE expression RSQUARE
           | (DesignatorFactor) designator
           | (MethodCall) method_call_ident actual_parameter_section RPAREN;

// ***************** CONDITIONS *********************
condition ::= (CondExpression) condition_term_list;
  condition_term_list ::= (CondTermList) condition_term_list or_operator condition_term
                        | (CondTerminalTerm) condition_term;
     or_operator ::= (OperatorOr) OR;
    condition_term ::= (CondTerm) condition_factor_list;
  condition_factor_list ::= (CondFactorList) condition_factor_list and_operator condition_factor
                          | (TerminalCondFactor) condition_factor;
      and_operator ::= (OperatorAnd) AND;
    condition_factor ::= (TermCondFactor) expression
                       | (CondOpFactor) expression rel_operator expression;
  rel_operator ::= (OperatorEq) EQ
                 | (OperatorNeq) NEQ
                 | (OperatorGrt) GRT
                 | (OperatorGrteq) GRTEQ
                 | (OperatorLss) LSS
                 | (OperatorLeq) LEQ;
