package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import rs.ac.bg.etf.pp1.ast.*;
import java.io.*;

scan with {:
    Symbol s = this.getScanner().next_token();
    if (s != null && s.value != null)
       System.err.println(s.toString() + " " + s.value.toString());

    return s;
:}

// *****************  KEYWORDS **********************
terminal PROGRAM, CONST, VOID, BREAK, CLASS, IF, ELSE, NEW, RETURN, DO, WHILE, EXTENDS;
terminal CONTINUE, PRINT, READ;

// *****************  OPERATORS *********************
terminal PLUS, MINUS, MUL, DIV, MOD, EQ, NEQ, GRT, GRTEQ, LSS, LEQ, AND, OR, ASSIGN, INC, DEC;
terminal SEMI, COMMA, DOT, LPAREN, RPAREN, RBRACE, LBRACE, LSQUARE, RSQUARE;

// *****************  IDENTIFICATORS ****************
terminal int NUMBER;
terminal Boolean BOOL;
terminal char CHAR;
terminal String IDENT;

// *****************  NONTERMINALS  *****************
nonterminal program; nonterminal rs.etf.pp1.symboltable.concepts.Obj program_name;

nonterminal method_return_type; nonterminal rs.etf.pp1.symboltable.concepts.Struct type;

nonterminal declaration_section, declaration,
            const_declaration_line, const_id_list, const_identifier,
            var_declaration_line, var_declaration_list, var_id_list, var_id, var_identifier,
            class_declaration, class_identifier,
            method_decl_section, optional_method_decl_section, method_decl_list, method_decl,  method_definition,
            formal_parameter_section, formal_parameter_list, formal_parameter;
nonterminal rs.etf.pp1.symboltable.concepts.Obj const_id, method_identifier;
nonterminal rs.etf.pp1.symboltable.concepts.Obj constant, char_const, numeric_const, bool_const;

nonterminal statement_list, statement, print_statement, read_statement, return_statement;
nonterminal designator_statement;
nonterminal rs.etf.pp1.symboltable.concepts.Obj designator;
nonterminal addition_operator, multiplication_operator;
nonterminal rs.etf.pp1.symboltable.concepts.Struct term, factor, expression, addition_term_list, multiplication_factor_list;
// **************************************************
//
// PRODUCTIONS
//
// *****************  PROGRAM  **********************
program ::= (Program) PROGRAM program_name:pName declaration_section method_decl_section;
program_name ::= (ProgramName) IDENT:id;

// *****************  TYPE **************************
type ::= (Type) IDENT: typeName;
method_return_type ::= (MethodReturnType) type
                     | (VoidReturnType) VOID;

// *****************  DECLARATIONS ******************
declaration_section ::= (DeclarationSection) declaration_section declaration
                   | (NoDeclarations) /* epsilon */;

  declaration ::= (DeclarationConst) const_declaration_line
                | (DeclarationVar) var_declaration_line
                | (DeclarationClass) class_declaration;

    // *** CONSTANT DECLARATION
    const_declaration_line ::= (ConstDeclarationLine) CONST type: t const_id_list SEMI;
      const_id_list ::= (ConstIdList) const_id_list COMMA const_id
                      | (ConstIdTerm) const_id;
        const_id ::= (ConstId) const_identifier ASSIGN constant;
        const_identifier ::= (ConstIdentifier) IDENT: id;
        constant ::= (ConstNumber) numeric_const: var
                   | (ConstBool) bool_const: var
                   | (ConstChar) char_const: var;
          numeric_const ::= (NumericConst) NUMBER: var;
          bool_const ::= (BoolConst) BOOL: var;
          char_const ::= (CharConst) CHAR: var;

    // *** VARIABLES DECLARATION
    var_declaration_list ::= (VarDeclarationList) var_declaration_list var_declaration_line
                           | (NoVariableDeclarations) /* epsilon */;
      var_declaration_line ::= (VarDeclarationLine) type: t var_id_list SEMI;
      var_id_list ::= (VarIDList) var_id_list COMMA var_id
                    | (VarIDTerm) var_id;
        var_id ::= (VarID) var_identifier: ident
                 | (VarIDArray) var_identifier: ident LSQUARE RSQUARE;
           var_identifier ::= (VarIdentifier) IDENT: id;

    // *** CLASS DECLARATION
    class_declaration ::= (ClassDeclaration) CLASS class_identifier LBRACE var_declaration_list optional_method_decl_section RBRACE;
      class_identifier ::= (ClassIdentifier) IDENT: id;

    // *** METHOD DECLARATION
    optional_method_decl_section ::= (OptionalMethodDeclSection) method_decl_section
                                   | (NoMethodSection)/* epsilon */;
    method_decl_section ::= (MethodDeclSection) LBRACE method_decl_list RBRACE;
      method_decl_list ::= (MethodDeclList) method_decl_list method_definition
                         | (MethodTerm) /* epsilon */;
        method_decl ::= (MethodDeclaration) method_identifier LPAREN formal_parameter_section RPAREN var_declaration_list;
          method_identifier ::= (MethodIdentifier) method_return_type IDENT: id;
          formal_parameter_section ::= (FormalParameters) formal_parameter_list
                                     | (NoParameters) /* epsilon */;
            formal_parameter_list ::= (FormalParameterList) formal_parameter_list COMMA formal_parameter
                                    | (FormalTermParameter) formal_parameter;
              formal_parameter ::= (FormalParameter) type IDENT
                                 | (FormalParameterArray) type IDENT LSQUARE RSQUARE;
        method_definition ::= (MethodDefinition)  method_decl LBRACE statement_list RBRACE;

// ***************** STATEMENTS *********************
statement_list ::= (StatementList) statement_list statement
                 | (NoStatements) /* epsilon */;
  statement ::= designator_statement SEMI
              | read_statement SEMI
              |  print_statement SEMI
              | return_statement SEMI;
    return_statement ::= (ReturnStatement) RETURN expression;
    read_statement ::= (ReadStatement) READ LBRACE designator RBRACE;
    print_statement ::= (PrintStatement) PRINT LBRACE expression RBRACE
                      | (ComplexPrintStatement) PRINT LBRACE expression COMMA numeric_const RBRACE;

// ***************** DESIGNATORS ********************
designator_statement ::= (Assignment) designator ASSIGN expression
                       | (Increment) designator INC
                       | (Decrement) designator DEC;
  designator ::= (Designator) IDENT:id;

// ***************** EXPRESSIONS ********************
expression ::= (AddExpression) addition_term_list
             | (SimpleExpression) designator LBRACE RBRACE;
  addition_term_list ::= (TermList) addition_term_list addition_operator term
                       | (TerminalTerm) term
                       | (NegTerminalTerm) MINUS term;
    addition_operator ::= PLUS
                        | MINUS;
  term ::= (Term) multiplication_factor_list;
  multiplication_factor_list ::= (FactorList) multiplication_factor_list multiplication_operator factor
                               | (TerminalFactor) factor;
    multiplication_operator ::= MUL
                              | DIV
                              | MOD;
  factor ::= (ConstantFactor) constant
           | (ExpressionFactor) LPAREN expression RPAREN
           | (ConstructorFactor) NEW type
           | (ConstructorArrayFactor) NEW type LSQUARE expression RSQUARE;
